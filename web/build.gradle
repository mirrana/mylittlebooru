buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'nu.studer:gradle-jooq-plugin:1.0.+'
        classpath 'org.apache.derby:derby:10.11.1.1'
    }
    configurations.classpath {
        resolutionStrategy {                            // enforce a specific jOOQ version
            forcedModules = [
                    'org.jooq:jooq:3.7.+',
                    'org.jooq:jooq-meta:3.7.+',
                    'org.jooq:jooq-codegen:3.7.+'
            ]
        }
    }
}

apply plugin: 'war'
apply plugin: 'nu.studer.jooq'

dependencies {
    // Servlet
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.+'
    compile group: 'org.glassfish.jersey.core', name: 'jersey-server', version: '2.22.+'
    compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet', version: '2.22.+'
    compile group: 'org.glassfish.jersey.ext', name: 'jersey-servlet-portability', version: '2.22.+'
    compile group: 'org.glassfish.jersey.media', name: 'jersey-media-multipart', version: '2.22.+'
    compile group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: '2.22.+'

    // Servlet Filters
    compile 'org.tuckey:urlrewritefilter:4.0.+'

    // Security
    compile group: 'org.mindrot', name: 'jbcrypt', version: '0.3m'

    // Database
    runtime group: 'org.apache.derby', name: 'derby', version: '10.11.1.1'
    compile 'org.jooq:jooq:3.7.+'
    compile 'org.jooq:jooq-meta:3.7.+'
    compile 'org.jooq:jooq-codegen:3.7.+'
}

task explodedWar(type: Copy) {
    group = 'build'
    into new File(buildDir, 'exploded-war')
    with war
}

jooq {
    derby(sourceSets.main) {
        jdbc {
            driver = 'org.apache.derby.jdbc.EmbeddedDriver'
            url = 'jdbc:derby:Q:/Software Development/007 Projects/Java/000 Web Applications/mylittlebooru/dist/data/db;create=true'
            user = ''
            password = ''
            schema = 'APP'
            properties {
                property {
                    key = 'ssl'
                    value = 'true'
                }
            }
        }
        generator {
            name = 'org.jooq.util.DefaultGenerator'
            strategy {
                name = 'org.jooq.util.DefaultGeneratorStrategy'
                // ...
            }
            database {
                inputSchema = 'APP'
            }
            generate {
                pojos = true
                daos = true
            }
            target {
                packageName = 'com.abopu.booru.db.derby'
                directory = new File(project(':web').projectDir, 'src/main/java')
            }
        }
    }
}